apply plugin: 'com.android.application'
//系统签名 配置
def keystoreProps = new Properties()
File keystoreProperties = rootProject.file("keystore.properties")
if (keystoreProperties.exists()) {
    keystoreProps.load(new FileInputStream(keystoreProperties))
} else {
    keystoreProps['keystorefile'] = 'none'
}

android {
    //通过config.gradle来宏定义版本
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    defaultConfig {
        applicationId "com.plat.sdk"
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
    }
    /*系统签名的值 包括debug和release两个版本*/
    signingConfigs {
        debug {
            storeFile file(keystoreProps.keyStore)
            storePassword keystoreProps.keyStorePassword
            keyAlias keystoreProps.keyAlias
            keyPassword keystoreProps.keyAliasPassword
        }
        relealse {
            storeFile file(keystoreProps.keyStore)
            storePassword keystoreProps.keyStorePassword
            keyAlias keystoreProps.keyAlias
            keyPassword keystoreProps.keyAliasPassword
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources false
            proguardFiles GLOBAL_PROGUARD_RULES_FILE
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources false
            /*GLOBAL_PROGUARD_RULES_FILE 存放在gradle.properties 用于混淆，防止反编译*/
            proguardFiles GLOBAL_PROGUARD_RULES_FILE
            signingConfig signingConfigs.relealse
        }
    }
    // 如果abortOnError为 true，则当lint发现错误时停止 gradle构建
    lintOptions { abortOnError false }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    //理解不是很清晰
    flavorDimensions "features"
    productFlavors {
        // 线程相关Demo
        basemvp {
            dimension "features"
            applicationId "com.flavors.mvp"
            manifestPlaceholders = ["Demo_DeviceVersion_Value": "${buildTime()}"]
            versionCode 1
            versionName "1.0.1"
        }
        //
        multithreading {
            dimension "features"
            applicationId "com.flavors.xiaomi"
            manifestPlaceholders = ["Demo_DeviceVersion_Value": "${buildTime()}"]
            versionCode 1
            versionName "1.0.1"
        }

        myLiveData {
            dimension "features"
            applicationId "com.flavors.livedata"
            manifestPlaceholders = ["Demo_DeviceVersion_Value": "${buildTime()}"]
            versionCode 1
            versionName "1.0.1"
        }
        /**
         * BitMap的加载和Cache
         * https://github.com/wowo3129/android-art-res/tree/master/Chapter_12
         */
        imageLoader {
            dimension "features"
            applicationId "com.flavors.imageLoader"
            manifestPlaceholders = ["Demo_DeviceVersion_Value": "${buildTime()}"]
            versionCode 1
            versionName "1.0.1"
        }

        SpeechiflylibDemo {
            dimension "features"
            applicationId "com.flavors.ifly"
            manifestPlaceholders = ["Demo_DeviceVersion_Value": "${buildTime()}"]
            versionCode 1
            versionName "1.0.1"
        }

    }

    //修改生成的apk名字
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = outputFile.name.replace("app-", "RS_${variant.versionName}_${buildTime().replace('-', '.')}_")
                fileName = fileName.replace("-release", "")
//                fileName = fileName.replace("-debug", "")
                outputFileName = fileName
            }
        }
    }
}

static def buildTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.jakewharton:butterknife:10.1.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'

    //网络部分
    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.okio:okio:1.15.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.1'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'

    //Android Architecture Component依赖 ydong
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    annotationProcessor 'androidx.lifecycle:lifecycle-compiler:2.0.0'
    testImplementation 'androidx.arch.core:core-testing:2.0.1'

    //权限添加
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar'
    //moudle
    implementation project(path: ':iflylib')
}
