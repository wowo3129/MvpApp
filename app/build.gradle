apply plugin: 'com.android.application'
//系统签名 配置
def keystoreProps = new Properties()
File keystoreProperties = rootProject.file("keystore.properties")
if (keystoreProperties.exists()) {
    keystoreProps.load(new FileInputStream(keystoreProperties))
} else {
    keystoreProps['keystorefile'] = 'none'
}

android {
    //通过config.gradle来宏定义版本
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    defaultConfig {
        applicationId "com.plat.sdk"
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
    }
    /*系统签名的值 包括debug和release两个版本*/
    signingConfigs {
        debug {
            storeFile file(keystoreProps.keyStore)
            storePassword keystoreProps.keyStorePassword
            keyAlias keystoreProps.keyAlias
            keyPassword keystoreProps.keyAliasPassword
        }
        relealse {
            storeFile file(keystoreProps.keyStore)
            storePassword keystoreProps.keyStorePassword
            keyAlias keystoreProps.keyAlias
            keyPassword keystoreProps.keyAliasPassword
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources false
            proguardFiles GLOBAL_PROGUARD_RULES_FILE
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources false
            /*GLOBAL_PROGUARD_RULES_FILE 存放在gradle.properties 用于混淆，防止反编译*/
            proguardFiles GLOBAL_PROGUARD_RULES_FILE
            signingConfig signingConfigs.relealse
        }
    }
    // 如果abortOnError为 true，则当lint发现错误时停止 gradle构建
    lintOptions { abortOnError false }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    //理解不是很清晰
    flavorDimensions "features"
    productFlavors {
        // 线程相关Demo
        basemvp {
            dimension "features"
            applicationId "com.flavors.mvp"
            manifestPlaceholders = ["Demo_DeviceVersion_Value": "${buildTime()}"]
            versionCode 1
            versionName "1.0.1"
        }
        //
        multithreading {
            dimension "features"
            applicationId "com.flavors.xiaomi"
            manifestPlaceholders = ["Demo_DeviceVersion_Value": "${buildTime()}"]
            versionCode 1
            versionName "1.0.1"
        }

        myLiveData {
            dimension "features"
            applicationId "com.flavors.livedata"
            manifestPlaceholders = ["Demo_DeviceVersion_Value": "${buildTime()}"]
            versionCode 1
            versionName "1.0.1"
        }

    }

    //修改生成的apk名字
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = outputFile.name.replace("app-", "RS_${variant.versionName}_${buildTime().replace('-', '.')}_")
                fileName = fileName.replace("-release", "")
//                fileName = fileName.replace("-debug", "")
                outputFileName = fileName
            }
        }
    }
}

static def buildTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

def retrofit_version = "2.1.0"
def rxjava_version = "2.0.1"

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    //zhangxutong add:
    //RxJava的依赖包
    implementation 'io.reactivex.rxjava2:rxjava:' + rxjava_version
    //RxAndroid的依赖包
    implementation 'io.reactivex.rxjava2:rxandroid:' + rxjava_version
    //Rxjava 和 Retrofit 的依赖库，将接口数据转换为rxjava数据形式供我们使用
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0-RC3'
    //Retrofit 依赖
    implementation 'com.squareup.retrofit2:retrofit:' + retrofit_version
    //自动将服务器数据给我们转换成javabean实体类
    implementation 'com.squareup.retrofit2:converter-gson:' + retrofit_version
    implementation 'com.google.code.gson:gson:2.6.2'
    //okhttp日志拦截器
    implementation 'com.squareup.okhttp3:logging-interceptor:3.3.0'

    //Android Architecture Component依赖 ydong
    implementation "android.arch.lifecycle:extensions:1.1.0"
    annotationProcessor "android.arch.lifecycle:compiler:1.1.0"
    testImplementation "android.arch.core:core-testing:1.1.0"
    //ydong
    //权限添加
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar'
    implementation project(path: ':iflylib')
}
